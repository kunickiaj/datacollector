buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.2"
  }
}

allprojects {
  apply plugin: 'maven'

  group = 'com.streamsets'
  version = '3.5.0-SNAPSHOT'

  // Add JUnit to test scope for ll projects using the Java plugin
  plugins.withType(JavaPlugin) {
    dependencies {
      compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
      testCompile 'junit:junit:4.12'
      testCompile 'org.mockito:mockito-core:1.10.19'
    }
  }

  repositories {
    mavenLocal()

    maven { url "https://repo.streamsets.net/artifactory/libs-release" }
    maven { url "https://repo.streamsets.net/artifactory/libs-snapshot" }
    maven { url "https://repository.cloudera.com/content/groups/cloudera-repos" }
    maven { url "http://packages.confluent.io/maven/" }
    maven { url "https://artifacts.elastic.co/maven" }
    maven { url "http://repository.mapr.com/maven/" }
    maven { url "http://repo.hortonworks.com/content/repositories/releases/" }
    maven { url "http://repo.hortonworks.com/content/repositories/re-hosted/" }
    maven { url "http://repo.hortonworks.com/content/repositories/jetty-hadoop/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://files.kinetica.com/nexus/content/repositories/releases/" }
    maven { url "http://repository.mapr.com/nexus/content/repositories/releases" }
//    maven { url "http://bits.cloudera.com/f93c6c9d/cdh6/6.0.0-beta1/maven-repository/" }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'scala'

  // Used for packaging stage libraries. the java-library-distribution plugin puts only runtime deps in the lib dir
  apply plugin: 'distribution'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  // The current annotation processor expects a slightly different configuration, as determined by the maven resources plugin
  sourceSets {
    main {
      output.resourcesDir = "$buildDir/classes/java/$name"
    }
    test {
      output.resourcesDir = "$buildDir/classes/java/$name"
    }
  }
}

def coreStageLibraryNames = [
    "basic-lib",
    "dataformats-lib"
]

def allStageLibraryNames = coreStageLibraryNames + [
    "aws-lib"
]

def coreStageLibraries = subprojects.findAll { coreStageLibraryNames.contains(it.name - 'streamsets-datacollector-') }
def allStageLibraries = subprojects.findAll { allStageLibraryNames.contains(it.name - 'streamsets-datacollector-') }
def protolibs = subprojects.findAll { it.name.contains("protolib") }

configure(allStageLibraries + protolibs) {
  // All stage libraries depend on these
  dependencies {
    // Standard 'provided' dependencies -- container provides these.
    compileOnly group: 'com.streamsets', name: 'streamsets-datacollector-api', version: '3.5.0-SNAPSHOT'
    compileOnly group: 'log4j', name: 'log4j', version: '1.2.17'
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compileOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.7'
    compileOnly group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.2'

    // Common dependencies required for test compilation
    testCompile group: 'com.streamsets', name: 'streamsets-datacollector-api', version: '3.5.0-SNAPSHOT'
    testCompile project(':streamsets-datacollector-root-proto:streamsets-datacollector-root:streamsets-datacollector-sdk')
    testCompile 'org.awaitility:awaitility:2.0.0'
  }
  // Used for packaging stage libraries. the java-library-distribution plugin puts only runtime deps in the lib dir
  // It extends the distribution plugin so we simply reconfigure the distribution plugin ourselves
  // ref: https://discuss.gradle.org/t/usage-of-distribution-plugin/12874/6
  distributions {
    main {
      contents {
        into('lib') {
          from jar // the stage library jar
          from(project.configurations.runtime) // the runtime dependencies
        }
      }
    }
  }
}

configurations {
  api
  container
  rootClasspath
}

dependencies {
  rootClasspath "org.xerial.snappy:snappy-java:1.0.5"
  api(
      "com.streamsets:streamsets-datacollector-api:$version",
      "org.slf4j:jul-to-slf4j:1.7.7"
  )
  container(
      project(':streamsets-datacollector-root-proto:streamsets-datacollector-root:streamsets-datacollector-container'),
      project(':streamsets-datacollector-root-proto:streamsets-datacollector-root:streamsets-datacollector-container-common'),
  )
}

apply plugin: 'distribution'
import org.apache.tools.ant.filters.ReplaceTokens

distributions {
  core {
    baseName = 'streamsets-datacollector-core'
    contents {
      // Can be refactored later
      into('') {
        from('dist/src/main') {
          include "etc/**"
          eachFile { file ->
            if (file.name.contains("password") || file.name.contains("realm")) {
              file.setMode(0600)
            }
          }
        }
      }
      into('') {
        from('dist/src/main') {
          include "bin/**"
          include "libexec/**"
          filter(ReplaceTokens, tokens: ['project.version': project.property("version")])
        }
        fileMode = 0755
      }
      // Default logging configuration
      into('etc') {
        from 'container/src/main/resources/sdc-log4j.properties-default'
        rename 'sdc-log4j.properties-default', 'sdc-log4j.properties'
      }
      // Empty directories
      into('') {
        File.createTempDir().with {
          ['data', 'libs-common-lib', 'log', 'resources', 'streamsets-libs-extras', 'user-libs'].forEach { dir ->
            def file = new File(absolutePath, dir)
            file.mkdirs()
          }
          from(absolutePath) {
            includeEmptyDirs = true
          }
        }
      }
      into('api-lib') {
        from configurations.api
      }
      into('libexec/bootstrap-libs/main') {
        from 'bootstrap/build/libs'
      }
      into('cli-lib') {
        from 'cli/build/libs'
      }
      into('container-lib') {
        from configurations.container
      }
      into('root-lib') {
        from configurations.rootClasspath
      }
      // The UI
      into('sdc-static-web') {
        from('datacollector-ui/target/dist')
      }
      // TODO: include docs
      // Extract any libs we want to include in the core tarball
      into('streamsets-libs') {
        coreStageLibraries.forEach {
          def tars = it.tasks.withType(Tar)
          tars.forEach { tar ->
            println("Including stage library: ${it.name}")
            from tarTree(tar.archivePath)
          }
        }
      }
    }
  }
}
